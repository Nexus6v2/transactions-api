plugins {
    id 'java'
    id 'idea'
    id 'groovy'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

repositories {
    mavenCentral()
}

version = '1.0.0'
sourceCompatibility = '1.8'

dependencies {
    // Vert.x
    compile "io.vertx:vertx-rx-java:3.5.2"
    compile "io.vertx:vertx-web:3.5.2"

    // Redis
    compile 'biz.paluch.redis:lettuce:4.4.0.Final'
    compile 'com.github.kstyrc:embedded-redis:0.6'

    // Utils
    compile 'org.apache.commons:commons-pool2:2.4.3'

    // Serialization
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.3'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'

    // Tests
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'org.skyscreamer:jsonassert:1.5.0'
}

mainClassName = 'io.vertx.core.Launcher'
def mainVerticle = 'io.bank.api.transactions.verticles.MainVerticle'

shadowJar {
    classifier = ''

    manifest {
        attributes 'Main-Verticle': mainVerticle
    }

    mergeServiceFiles {
        include 'META-INF/handlers/io.vertx.core.spi.VerticleFactory'
    }
}

run {
    args = ['run', mainVerticle]
}

compileJava {
    options.incremental = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8'
}

lombok {
    version = "1.16.20"
    sha256 = "c5178b18caaa1a15e17b99ba5e4023d2de2ebc18b58cde0f5a04ca4b31c10e6d"
}